if (rootProject.ext.IS_DEBUG_MODULE_arouter) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}


android {
    compileSdkVersion 28



    defaultConfig {
        /** ARouter配置 */
        if (rootProject.ext.IS_DEBUG_MODULE_arouter) {
            applicationId "cc.catface.arouter_demo"
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }


        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        /** 方法数64k */
        multiDexEnabled true
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }

    sourceSets {
        main {
            if (rootProject.ext.IS_DEBUG_MODULE_arouter) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    exclude '**/debug/**'
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    /** */
    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.appcompat:appcompat:1.1.0-alpha01"
    testImplementation "junit:junit:4.12"
    androidTestImplementation "androidx.test:runner:1.1.2-alpha01"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.1.2-alpha01"
    annotationProcessor 'androidx.annotation:annotation:1.0.1'
    implementation project(':app_base')

    /* ARouter */
    api "com.alibaba:arouter-api:1.4.1"
    annotationProcessor "com.alibaba:arouter-compiler:1.2.2"

    implementation 'com.jakewharton:butterknife:10.0.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'
}

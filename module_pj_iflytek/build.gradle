if (rootProject.ext.debug_module_pj_iflytek) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        ndk {
            abiFilters("armeabi")
        }

        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
        if (rootProject.ext.debug_module_pj_iflytek) {
            applicationId "cc.catface.iflytek"
        }
    }

    sourceSets {
        main {
            if (rootProject.ext.debug_module_pj_iflytek) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    exclude '**/debug/**'
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    /** data binding & java1.8 */
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    annotationProcessor 'androidx.annotation:annotation:1.1.0'
    annotationProcessor "com.alibaba:arouter-compiler:1.2.2"
    implementation project(':app_base')
    implementation files('libs/AIUI.jar')
    implementation files('libs/mail.jar')
    implementation files('libs/activation.jar')
    implementation files('libs/additionnal.jar')
    api files('libs/Msc.jar')
    implementation files('libs/commons-codec-1.13.jar')
}
